GWENDOLEN

:name: lrmate

:Initial Beliefs:
gripper("open")

gear_tray(sku_medium_gear_vessel16, "medium", ["sku_part_medium_gear17",
	"sku_part_medium_gear18",
	"sku_part_medium_gear19",
	"sku_part_medium_gear20"])

gear_tray(sku_large_gear_vessel21, "large", ["sku_part_large_gear22","sku_part_large_gear23"])

kit_tray(sku_kit_m2l1_vessel14,[slot("sku_kit_m2l1_vessel14.slot1","medium","empty"),
	slot("sku_kit_m2l1_vessel14.slot2","medium","empty"),
	slot("sku_kit_m2l1_vessel14.slot3","large","empty")])

kit_tray(sku_kit_m2l1_vessel15,[slot("sku_kit_m2l1_vessel15.slot1","medium","empty"),
	slot("sku_kit_m2l1_vessel15.slot2","medium","empty"),
	slot("sku_kit_m2l1_vessel15.slot3","large","empty")])

:Reasoning Rules:

:Initial Goals:
new_tray [perform]

:Plans:

+!new_tray [perform] : { B kit_tray(IdKitTray,Slots) }
<-
        trace("new_tray"), 
	find_slot(Slots),
	-kit_tray(IdKitTray,Slots),
	+!loop [perform];

+!loop [perform] : { B slot_active(Id, SizeSlot, Slot) }
<-
        trace("loop perform"), 
	-slot_active(Id, SizeSlot, Slot),
	+!kitting(Id, SizeSlot) [perform],
	+!loop [perform];

+!kitting(Id, Size)  [perform] : { ~B grasped(_), B gear_tray(IdGearTray, Size, Slots) }
<-
        trace("kitting"), 
	find_gear(Slots),
	*gear(Gear),
	+!take_part(Gear) [perform],
	+!place_part(Id,Gear) [perform];

+slot(Id, SizeSlot, Slot) : { True }
<-	
        trace("slot Id SizeSlot Slot "), 
       +slot_active(Id, SizeSlot, Slot);

+humanProximityViolation   : { B humanProximityViolation}
<-
        print("detect_human_proximity_violation"), 
        remove_belief("humanProximityViolation");
	
+!loop [perform] : { B kit_tray(IdKitTray,Slots) } 
<-
      trace("loop new_tray"), 
      printPercepts(1),	
      +!new_tray [perform];


+!loop [perform] : { True }
<-
        trace("loop done T no beliefs"), 
        printWorld(1),
	printPercepts(1),	
        wait(2000),
	+!loop [perform];
	
+!take_part(Location) [perform] : { ~B grasped(_), B gripper("open")}
<-
        trace("take_part"), 
 	take_part(Location),
	 *action_result(Result),
	 +!check_action_result_take_part(Location, Result) [perform];

+!place_part(Location, Gear) [perform] : { B grasped(Gear), B gripper("close")}
<-
        trace("place_part"), 
 	place_part(Location),
	 *action_result(Result),
	 +!check_action_result_place_part(Location, Gear, Result) [perform];

+!check_action_result_take_part(Location, "true") [perform] : { True } 
<- 
 	 trace("check_action_result_take_part T"), 
	 -gripper("open"),
	 +gripper("close"),
	 +grasped(Gear);


+!check_action_result_take_part(Location, "droppedGear") [perform] : { True}
<-
         trace("check_action_result_take_part droppedGear"),
	 reachable_gear(Location),
	 *action_result(Result),
	 +!check_action_result_take_part(Location, Result) [perform];

+!check_action_result_take_part(Location, "reachableGear") [perform] : { True}
<-
         trace("check_action_result_take_part reachableGear"),
	 take_part(Location),
	 *action_result(Result),
	 +!check_action_result_take_part(Location, Result) [perform];

+!check_action_result_take_part(Location, "abortGear") [perform] : { True }
<-
         trace("check_action_result_take_part abortGear");


+!check_action_result_take_part(Location, "false") [perform] : { True }
<-
         trace("check_action_result_take_part F"),
	 take_part(Location),
	 *action_result(Result),
	 +!check_action_result_take_part(Location, Result) [perform];
	 
+!check_action_result_place_part(Location, Gear, "true") [perform] : { True } 
<-         
         trace("check_action_result_place_part T"),
	 -grasped(Gear),
	 -gripper("close"),
	 +gripper("open");

+!check_action_result_place_part(Location, Gear, "false") [perform] : { True }
<-
         trace("check_action_result_place_part"),
	 place_part(Location, Gear),
	 *action_result(Result),
	 +!check_action_result_place_part(Location, Gear, Result) [perform];
