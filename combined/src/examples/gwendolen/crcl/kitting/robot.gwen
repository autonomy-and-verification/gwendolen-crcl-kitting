GWENDOLEN

:name: robot

:Initial Beliefs:
kit_tray(kit_tray_1,[slot("kit_slot_1","small","empty"),slot("kit_slot_2","small","empty"),slot("kit_slot_3","medium","empty"),slot("kit_slot_4","large","empty")])

:Reasoning Rules:

:Initial Goals:
new_tray [perform]

:Plans:

+!kitting(Id, Size)  [perform] : { ~B grasped(_), B gear_tray(IdGearTray, Size, Slots) }
<-
	find_gear(IdGearTray, Size, Slots),
	*gear(Gear),
	+!move(Gear) [perform],
	+!close_gripper(Gear) [perform],
	+!move(Id) [perform],
	+!open_gripper(Gear) [perform];

+!new_tray [perform] : { B kit_tray(IdKitTray,Slots) }
<-
	find_slot(Slots),
	-kit_tray(IdKitTray,Slots),
	+!loop [perform];
	
+slot(Id, SizeSlot, Slot) : { True }
<-	+slot_active(Id, SizeSlot, Slot);
	
+!loop [perform] : { B slot_active(Id, SizeSlot, Slot) }
<-
	-slot_active(Id, SizeSlot, Slot),
	+!kitting(Id, SizeSlot) [perform],
	+!loop [perform];
+!loop [perform] : { True }
<-
	wait(2000),
	+!loop [perform];
	
+!move(Location) [perform] : { True }
<-
	 move(Location),
	 *action_result(Result),
	 +!check_action_result_move(Location, Result) [perform];
	 
+!close_gripper(Gear) [perform] : { ~B grasped(_), B gripper("open") }
<-
	 close_gripper,
	 *action_result(Result),
	 +!check_action_result_close_gripper(Result) [perform];
	 
+!open_gripper(Gear) [perform] : { B grasped(Gear), B gripper("closed") }
<-
	 open_gripper,
	 *action_result(Result),
	 +!check_action_result_open_gripper(Result) [perform];
	 
+!check_action_result_move(Location, "true") [perform] : { True };
+!check_action_result_move(Location, "false") [perform] : { True }
<-
	 move(Location),
	 *action_result(Result),
	 +!check_action_result_move(Location, Result) [perform];
	 
+!check_action_result_open_gripper("true") [perform] : { True };
+!check_action_result_open_gripper("false") [perform] : { True }
<-
	 open_gripper,
	 *action_result(Result),
	 +!check_action_result_open_gripper(Result) [perform];
	 
+!check_action_result_close_gripper("true") [perform] : { True };
+!check_action_result_close_gripper("false") [perform] : { True }
<-
	 close_gripper,
	 *action_result(Result),
	 +!check_action_result_close_gripper(Result) [perform];