GWENDOLEN

:name: robot

:Initial Beliefs:
gripper("open")
gear_tray(gear_tray_1,"small",["gear_small_1","gear_small_2","gear_small_3"])
gear_tray(gear_tray_1,"medium",["gear_medium_1","gear_medium_2","gear_medium_3"])
gear_tray(gear_tray_1,"large",["empty","gear_large_2","gear_large_3"])

:Reasoning Rules:

:Initial Goals:

:Plans:

+!kitting(IdKitTray, Id, Size)  [perform] : { ~B grasped(_), B gear_tray(IdGearTray, Size, Slots) }
<-
	find_gear(Slots),
	*gear(Gear),
	+!move(Gear) [perform],
	+!close_gripper(Gear) [perform],
	+!move(Id) [perform],
	+!open_gripper(Gear) [perform];

+kit(IdKitTray, Id, SizeSlot) : { True }
<-
	+!kitting(IdKitTray, Id, SizeSlot) [perform];
	
+!move(Location) [perform] : { True }
<-
	 move(Location),
	 *action_result(Result),
	 -action_result(Result),
	 +!check_action_result_move(Location, Result) [perform];
	 
+!close_gripper(Gear) [perform] : { ~B grasped(_), B gripper("open") }
<-
	 close_gripper,
	 *action_result(Result),
	 -action_result(Result),
	 -gripper("open"),
	 +gripper("close"),
	 +grasped(Gear),
	 +!check_action_result_close_gripper(Result) [perform];
	 
+!open_gripper(Gear) [perform] : { B grasped(Gear), B gripper("close") }
<-
	 open_gripper,
	 *action_result(Result),
	 -action_result(Result),
	 -gripper("close"),
	 +gripper("open"),
	 +!check_action_result_open_gripper(Result) [perform];
	 
+!check_action_result_move(Location, "true") [perform] : { True };
+!check_action_result_move(Location, "false") [perform] : { True }
<-
	 move(Location),
	 *action_result(Result),
	 -action_result(Result),
	 +!check_action_result_move(Location, Result) [perform];
	 
+!check_action_result_open_gripper("true") [perform] : { True };
+!check_action_result_open_gripper("false") [perform] : { True }
<-
	 open_gripper,
	 *action_result(Result),
	 -action_result(Result),
	 +!check_action_result_open_gripper(Result) [perform];
	 
+!check_action_result_close_gripper("true") [perform] : { True };
+!check_action_result_close_gripper("false") [perform] : { True }
<-
	 close_gripper,
	 *action_result(Result),
	 -action_result(Result),
	 +!check_action_result_close_gripper(Result) [perform];